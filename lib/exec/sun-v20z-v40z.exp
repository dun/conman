#!/usr/bin/expect -f
###############################################################################
# Written by Chris Dunlap <cdunlap@llnl.gov>.
# Copyright (C) 2007-2016 Lawrence Livermore National Security, LLC.
# Copyright (C) 2001-2007 The Regents of the University of California.
# UCRL-CODE-2002-009.
#
# This file is part of ConMan: The Console Manager.
# For details, see <https://dun.github.io/conman/>.
###############################################################################
# This script connects to a console managed by the service processor on a
# Sun Fire V20z/V40z server using the SSH protocol.
#
# This script can be specified in "conman.conf" in the following manner:
#
#   console name="zot" dev="/path/to/sun-v20z-v40z.exp HOST USER PSWD"
#
# HOST is the hostname of the Sun Fire server.
# USER is the username being authenticated.
# PSWD is the corresponding password.
#
# Since this command-line will persist in the process listing for the duration
# of the connection, passing sensitive information like PSWD in this manner is
# not recommended.  Instead, consider using either a command-line argument
# default or the password database (see below).
###############################################################################

##
# Set "exp_internal" to 1 to print diagnostics describing internal operations.
#   This is helpful in diagnosing pattern-match failures.
##
  exp_internal 0

##
# Set "log_user" to 1 to show the underlying dialogue establishing a connection
#   to the console.
##
  log_user 0

##
# The "timeout" specifies the number of seconds before the connection attempt
#   times-out and terminates the connection.
##
  set timeout 10

##
# If "session_override" is enabled, an existing connection to this console
#   session will be forced into "spy" mode (ie, made read-only) while the new
#   connection is attached in read-write mode.  Subsequent attempts to steal
#   write-privileges from this console session will be thwarted.
# Otherwise, an existing connection to this console session will cause the new
#   connection to enter "spy" mode.  No attempts will be made to steal
#   write-privileges.
# Beware of dueling banjos if a given console session is overridden by more
#   than one process, as these processes will continuously steal the console
#   session from each other.
##
  set session_override 1

##
# The "password_db" specifies the location of the password database.
#   This avoids exposing sensitive information on the command-line without
#   needing to modify this script.
# Whitespace and lines beginning with '#' are ignored.  The file format is:
#   <host-regex> : <user> : <pswd>
##
  set password_db "/etc/conman.pswd"

##
# Command-line argument defaults can be specified here.  This avoids exposing
#   sensitive information on the command-line.
##
# set user "admin"
# set pswd "admin"

###############################################################################

set env(PATH) "/usr/bin:/bin"

proc get_password {host user index} {
  global password_db
  set db_pswd {}
  if {! [info exists password_db]} {
    return
  }
  if {[catch {open $password_db} input]} {
    return
  }
  while {[gets $input line] != -1} {
    if {[regexp {^[ \t]*#} $line]} {
      continue
    }
    set record [split $line ":"]
    set db_host [string trim [lindex $record 0]]
    if {[catch {regexp "^$db_host$" $host} got_host_match]} {
      continue
    }
    if {! $got_host_match && [string length $db_host]} {
      continue
    }
    set db_user [string trim [lindex $record 1]]
    if {[string compare $db_user $user]} {
      continue
    }
    set db_pswd [string trim [lindex $record $index]]
    break
  }
  close $input
  return $db_pswd
}

if {! $argc} {
  set prog [lindex [split $argv0 "/"] end]
  send_user "Usage: $prog <host> <user> <pswd>\r\n"
  exit 1
}
if {$argc > 0} {
  set host [lindex $argv 0]
}
if {$argc > 1} {
  set user [lindex $argv 1]
}
if {$argc > 2} {
  set pswd [lindex $argv 2]
}
set authenticated 0
set connected 0
if {! [info exists host]} {
  send_user "Error: Unspecified hostname.\r\n"
  exit 1
}
if {! [info exists user]} {
  send_user "Error: Unspecified username.\r\n"
  exit 1
}
if {! [info exists pswd]} {
  set pswd [get_password $host $user 2]
  if {! [string length $pswd]} {
    send_user "Error: Unspecified password.\r\n"
    exit 1
  }
}
if {! [info exists session_override]} {
  set session_override 0
}
set override $session_override
if {[catch "spawn ssh -a -e \& -l $user -p 22 -x $host platform console" spawn_result]} {
  send_user "Error: $spawn_result.\r\n"
  exit 1
}
expect {
  -re "\\\[Enter \[^]]* for help]\r+\n" {
    incr connected
    set timeout 1
    exp_continue
  }
  -re "\\\[no, (\[^]]*) is attached]\r+\n" {
    if {$session_override == 0} {
      send_user "Attaching read-only: $expect_out(1,string) already attached.\r\n"
    } else {
      send "\005cf\r"
      expect -re "\\\[bumped \[^]]*]\r+\n"
      exp_continue
    }
  }
  -re "\\\[forced to `spy' mode by (\[^]]*)]\r+\n" {
      send_user "Attaching read-only: unable to override $expect_out(1,string).\r\n"
  }
  -gl "Permission denied" {
    send_user "Error: Permission denied.\r\n"
    exit 1
  }
  -re "^ssh: (\[^\r]*)\r+\n" {
    send_user "Error: $expect_out(1,string).\r\n"
    exit 1
  }
  eof {
    send_user "Error: Connection closed by remote host.\r\n"
    exit 1
  }
  timeout {
    if {$connected == 0} {
      send_user "Error: Timed-out.\r\n"
      exit 1
    }
  }
  -nocase -gl "Are you sure you want to continue connecting (yes/no)? \$" {
    send "yes\r"
    exp_continue -continue_timer
  }
  -nocase -gl "Password: \$" {
    if {$authenticated == 0} {
      send "$pswd\r"
      incr authenticated
      exp_continue -continue_timer
    } else {
      send_user "Error: Permission denied.\r\n"
      exit 1
    }
  }
  -re "\r+\n" {
    exp_continue -continue_timer
  }
}
send_user "Connection established via ssh (pid $spawn_result).\r\n"

set timeout 2
interact {
  # Replace "&B" with "l0" serial-break as per config file.
  "&B" {
    send "\005cl0\r"
    expect -re "\\\[halt sent]\r+\n"
  }
  # Match subsequent patterns against spawned process, not user's keystrokes.
  -o
  # Prevent theft of console if "session_override" is enabled; o/w, spy.
  #   Beware of dueling banjos.
  -re "\\\[forced to `spy' mode by \[^]]*]\r+\n" {
    if {$session_override == 0} {
      send_user "$interact_out(0,string)"
    } else {
      send "\005cf\r"
      expect -re "\\\[bumped \[^]]*]\r+\n"
    }
  }
}
